- 合约基本用法
- 合约的构造函数使用
- 函数的基本用法
- 事件和事件的触发
- NatSpec用法演示
- ’fallback‘ 和 ’receive‘的使用和不同之处
- ’immutable‘不可变量的使用
- ’constant‘常量的使用
- unicode字面常量
- modifier使用
- ’keccak256‘结合’abi.encodePacked‘判断字符串是否相同

 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract HelloComrades {
    /*
     *state variables
     *
     */
    /// @notice 用于标记当前进度
    /// @dev  0: 等待领导发话
    /// @dev  1: 等待同志回复
    /// @dev  2: 等待领导再次发话
    /// @dev  3: 等待同志再次回复
    /// @dev  4: 等待合约销毁
    /// @return 当前进度
    uint8 public step = 0;

    address public immutable leader;
    
    string internal constant UNKNOW = unicode"情况未知";

     /*
    //  * Event
     *
     */
    event Log(string tag,address from, uint256 value, bytes data);

    event Step(uint8);

    /*
     * Modifier
     *
     */
    /// @notice 检查只能领导调用
    /// @dev  用于领导使用的函数
    modifier onlyLeader() {
        require(msg.sender == leader, unicode"必须领导才可以");
        _;
    }
    /// @notice 检查不能领导调用
    /// @dev  不用于领导使用的函数
    modifier notLeader() {
        require(msg.sender != leader, unicode"必须群众说");
        _;
    }

    
     /*
     * Errors
     *
     */
    /// @notice 自定义错误
    /// @dev  所有的已知错误

    /// 这是一个自定义错误，上方需要空一行
    error MyError(string msg);

    /*
     * Function
     *
     */
    function hello(string calldata content) external onlyLeader returns(bool) {
        if(step != 0){
            // 报错
            revert(UNKNOW);
        }
        if(!review(content,unicode"同志们好")){
            // 报错
            revert MyError(unicode"必须说：同志们好");
        }
        step = 1;
        emit Step(step);
        return true;
    }

    
    function helloRes(string calldata content) external notLeader returns(bool) {
        if(step != 1){
            // 报错
            revert(UNKNOW);
        }
        if(!review(content,unicode"领导好")){
            // 报错
            revert MyError(unicode"必须说：领导好");
        }
        step = 2;
        emit Step(step);
        return true;
    }

    function comfort(string calldata content) external payable onlyLeader returns(bool) {
        if(step != 2){
            // 报错
            revert(UNKNOW);
        }
        if(!review(content,unicode"同志们辛苦了")){
            // 报错
            revert MyError(unicode"必须说：同志们辛苦了");
        }
        if((msg.value) < 2 ether ) {
            revert MyError(unicode"转账金额小于2个");
        }
        step = 3;
        emit Step(step);
        return true;
    }

     function comfortRes(string calldata content) external notLeader returns(bool) {
        if(step != 3){
            // 报错
            revert(UNKNOW);
        }
        if(!review(content,unicode"为人民服务")){
            // 报错
            revert MyError(unicode"必须说：为人民服务");
        }
        step = 4;
        emit Step(step);
        return true;
    }

    function desturct() external onlyLeader returns(bool) {
        if(step != 4){
            // 报错
            revert(UNKNOW);
        }
        emit Log("desturct", msg.sender,address(this).balance,msg.data);
        selfdestruct(payable(msg.sender));
        return true;
    }

    /*
     * Constructor
     *
     */
    constructor (address leader_) {
        require(leader_!=address(0), "invalid address");
        leader = leader_;
    } 

    /*
     * Helper
     *
     */
    function review(string calldata content, string memory correctContent) private pure returns(bool) {
        return keccak256(abi.encodePacked(content)) == keccak256(abi.encodePacked(correctContent));
    }

    receive () external payable {
        emit Log("receive", msg.sender,msg.value,"");
    }

    fallback () external payable {
        emit Log("fallback", msg.sender,msg.value,"");
    }

     /// @notice 获取当前合约的余额
     /// @dev   一个当前合约金额的辅助函数
     /// @return 当前余额
     function getBalance() external view returns(uint256) {
         return address(this).balance;
     }
}
```
